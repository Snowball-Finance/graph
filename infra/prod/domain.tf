data "aws_route53_zone" "snowapi_net" {
  name = "snowapi.net"
}

resource "aws_route53_record" "node" {
  name    = local.domain_name
  type    = "A"
  zone_id = data.aws_route53_zone.snowapi_net.zone_id
  alias {
    evaluate_target_health = false
    name                   = aws_alb.this.dns_name
    zone_id                = aws_alb.this.zone_id
  }
}

resource "aws_acm_certificate" "cert" {
  domain_name       = "${local.domain_name}.${data.aws_route53_zone.snowapi_net.name}"
  validation_method = "DNS"

  lifecycle {
    create_before_destroy = true
  }

  tags = {
    Name = "${local.env}-${local.project}-${local.node}-certificate"
  }
}

resource "aws_route53_record" "cert_validation_record" {
  for_each = {
    for dvo in aws_acm_certificate.cert.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  name            = each.value.name
  type            = each.value.type
  zone_id         = data.aws_route53_zone.snowapi_net.zone_id
  records         = [each.value.record]
  ttl             = 60
  allow_overwrite = true
}

resource "aws_acm_certificate_validation" "cert_validation" {
  certificate_arn         = aws_acm_certificate.cert.arn
  validation_record_fqdns = [for record in aws_route53_record.cert_validation_record : record.fqdn]
}
